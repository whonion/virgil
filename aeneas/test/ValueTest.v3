def TEST = UnitTest.new("Value", runAllTests);

def runAllTests() {
	testIntBox();
	testLongBox();
	testBoolBox();
	testBoxVal();
}

def BOTTOM = Values.BOTTOM;
def INT_ONE = Int.box(1);
def INT_TWO = Int.box(2);
def INT_MIN = Int.box(int.min);
def LONG_ONE = Long.box(1);
def LONG_MAX = Long.box(long.max);
def BOOL_TRUE = Bool.box(true);

def assertEq(a: Val, b: Val) {
	if (!Values.equal(a, b) || !Values.equal(b, a)) {
		var buf = StringBuilder.new();
		buf.puts("expected ");
		V3.renderResult(a, null, buf);
		buf.puts(" == ");
		V3.renderResult(b, null, buf);
		System.error(TEST.name, buf.toString());
	}
	var ha = Values.hash(a), hb = Values.hash(b);
	if (ha != hb) {
		var buf = StringBuilder.new();
		buf.puts("expected hash ");
		V3.renderResult(a, null, buf);
		buf.puts(" == ");
		V3.renderResult(b, null, buf);
		buf.put2(" (%d != %d)", ha, hb);
		System.error(TEST.name, buf.toString());
	}
}

def assertEqSet(a: Range<Val>) {
	for (i in a) for (j in a) assertEq(i, j);
}

def assertNeSet(a: Range<Val>) {
	for (i < a.length) for (j < a.length) {
		if (i == j) assertEq(a[i], a[j]);
		else assertNe(a[i], a[j]);
	}
}

def assertNe(a: Val, b: Val) {
	if (Values.equal(a, b) || Values.equal(b, a)) {
		var buf = StringBuilder.new();
		buf.puts("expected ");
		V3.renderResult(a, null, buf);
		buf.puts(" != ");
		V3.renderResult(b, null, buf);
		System.error(TEST.name, buf.toString());
	}
}

def testIntBox() {
	def box = Int.box;

	assertEqSet([BOTTOM, Int.ZERO, box(0), box(0)]);
	assertEqSet([Int.ONE, box(1), box(1)]);
	assertEqSet([Int.TWO, box(2), box(2)]);
	assertEqSet([Int.FOUR, box(4), box(4)]);
	assertEqSet([Int.MINUS_1, box(-1), box(-1)]);

	assertNeSet([Int.ZERO, Int.ONE, Int.TWO, Int.FOUR, Int.MINUS_1]);

	for (i in [99, -87, 89734, 1239, int.min, int.max]) {
		assertEqSet([box(i), box(i)]);
	}
}

def testLongBox() {
	def box: long -> Val = Long.box;

	assertEqSet([BOTTOM, box(0), box(0)]);

	var longs: Array<long> = [97, -88, 89733, 12398, long.min, long.max];
	for (i in longs) assertEqSet([box(i), box(i)]);

	var vals = Arrays.map(longs, box);
	assertNeSet(vals);
}

def testBoolBox() {
	var t = Bool.TRUE, f = Bool.FALSE;
	def box = Bool.box;
	assertEq(t, t);
	assertEq(f, f);

	assertEq(box(true), t);
	assertEq(box(false), f);

	assertNe(t, f);

	assertNe(t, box(false));
	assertNe(f, box(true));

	assertEqSet([BOTTOM, Bool.FALSE, box(false), box(false)]);
}

def box1(v: Val) -> Val {
	return BoxVal.new(null, [v]);
}

def box2(a: Val) -> Val {
	return BoxVal.new(null, [a, a]);
}

def testBoxVal() {
	var box = BoxVal.new(null, _);

	assertEqSet([box([]), box([])]);

	var vals: Array<Val> = [BOTTOM, INT_ONE, INT_MIN, BOOL_TRUE, LONG_MAX];
	for (v in vals) {
		assertEqSet([box1(v), box1(v)]);
		assertEqSet([box2(v), box2(v)]);
	}

	assertNeSet(Arrays.map(vals, box1));
	assertNeSet(Arrays.map(vals, box2));

	var zeroes = [BOTTOM, Int.ZERO, Int.box(0), Long.box(0), Bool.FALSE, Bool.box(false)];
	for (v in zeroes) {
		assertEqSet([null, box1(v), box2(v)]);
	}
	// TODO: more complex tests with records, closures, etc
}
