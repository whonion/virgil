def T = UnitTests.registerT("typesys:", _, TypeSystemTester.new, _);
def X_ = void(
	T("Implicit", testImplicit),
	T("Subtype", testSubtype),
	T("Upper", testUpper),
	T("Widen", testWiden),
	T("CommonSuperClass", testCommonSuperClass),
	T("Unify", testUnify),
	T("UnifyWiden", testUnifyWiden),
	T("NewTypeQuery", testNewTypeQuery),
	T("NewTypeCast", testNewTypeCast),
	T("MaybeEqual", testMaybeEqual),
	T("Floats", testFloats),
	()
);

// Some example types
def typeCache = TypeCache.new();
def BYTE = Byte.TYPE;
def I32 = Int.TYPE;
def I64 = Long.TYPE;
def BOOL = Bool.TYPE;
def NULL = Null.TYPE;
def VOID = Void.TYPE;
def STRING = V3.stringType;
def T_I32_I32 = t2(I32, I32);
def T_I64_I64 = t2(I64, I64);
def CLASS_A = cl("A", null);
def CLASS_B = cl("B", CLASS_A);
def CLASS_C = cl("C", CLASS_A);
def CLASS_X = cl("X", null);
def VARIANT_E = initVariantE();
var CASE_F: ClassType;
var CASE_G: ClassType;
def PARAM_P = tp("P");
def PARAM_Q = tp("Q");
def CLASS_Y_P = cl1("Y", PARAM_P, null);
def CLASS_Y_Q = ClassType.!(sub1(CLASS_Y_P, PARAM_Q));
def CLASS_Z_Q = cl1("Z", PARAM_Q, CLASS_Y_Q);
def CLASS_Z_P = ClassType.!(sub1(CLASS_Z_Q, PARAM_P));
def VAR_V = tv("V", PARAM_P);
def VAR_W = tv("W", PARAM_Q);
def FLOAT = Float.FLOAT32;
def DOUBLE = Float.FLOAT64;
def prim = [BYTE, I32, I64, BOOL];
def ARRAY_I32 = V3Array.newType(I32);
def ARRAY_A = V3Array.newType(CLASS_A);
def some = [BYTE, I32, I64, BOOL, NULL, VOID, STRING, ARRAY_I32, ARRAY_A];
def classes = [CLASS_A, CLASS_B, CLASS_C, CLASS_X, CLASS_Y_P, CLASS_Z_Q];
// TODO: some example variant, enum types
// TODO: some example function types

def t2(a: Type, b: Type) -> Type {
	return Tuple.newType(Lists.cons2(a, b));
}
def t3(a: Type, b: Type, c: Type) -> Type {
	return Tuple.newType(Lists.cons3(a, b, c));
}
def nn(f: (Type, Type) -> Type, a: Type, b: Type) -> Type {
	return if(a != null && b != null, f(a, b));
}
def t2n = nn(t2, _, _);
def f1n = nn(f1, _, _);
def f1(a: Type, r: Type) -> Type {
	return Function.newType(a, r);
}
def f2(a: Type, b: Type, r: Type) -> Type {
	return Function.newType(t2(a, b), r);
}
def fn(a: Type, b: Type) -> Type {
	if (a == null || b == null) return null;
	return Function.newType(a, b);
}
def cl(name: string, s: ClassType) -> ClassType {
	var tok = Token.new("<test>", name, 0, 0);
	var superclass = if(s != null, TypeUtil.typeRefOf(s));
	var decl = VstClass.new(false, tok, null, null, superclass, null, null);
	var typecon = V3Class_TypeCon.new(decl, typeCache);
	return ClassType.!(typecon.create0());
}
def cl1(name: string, typeParam: TypeParamType, s: ClassType) -> ClassType {
	var tok = Token.new("<test>", name, 0, 0);
	var superclass = if(s != null, TypeUtil.typeRefOf(s));
	var typeParams = List.new(typeParam, null);
	var typeEnv = TypeEnv.new(null, typeParams, null);
	var decl = VstClass.new(false, tok, typeParams, null, superclass, null, null);
	decl.typeEnv = typeEnv;
	var typecon = V3Class_TypeCon.new(decl, typeCache);
	var result = ClassType.!(typecon.create1(typeParam));
	if (s != result.getSuperType()) var x = 1/0;
	return result;
}
def sub1(t: Type, typeArg: Type) -> Type {
	if (1 != t.typeCon.arity) var x = 1/0;
	return t.typeCon.create1(typeArg);
}
def tv(name: string, tp: TypeParamType) -> TypeVarType {
	var tok = Token.new("<test>", name, 0, 0);
	var typecon = TypeVar_TypeCon.new(tok, tp, typeCache);
	return TypeVarType.!(typecon.create0());
}
def tp(name: string) -> TypeParamType {
	var tok = Token.new("<test>", name, 0, 0);
	return TypeUtil.newTypeParam(tok, typeCache);
}

def renderer(t: Type) -> StringBuilder -> StringBuilder {
	return if(t == null, renderNull, t.render);
}

def renderNull(buf: StringBuilder) -> StringBuilder {
	return buf.puts("{null}");
}

def initVariantE() -> ClassType {
	var idE = VstIdent<TypeParamType>.new(Token.new("<test>", "E", 0, 0), 0, null);
	var desugaring = VariantDesugaring.new(false, idE, null);

	var declF = desugaring.synthesizeVariantCaseMember(
		VstIdent.new(Token.new("<test>", "F", 0, 0), 0, null), null, null).decl;
	var declG = desugaring.synthesizeVariantCaseMember(
		VstIdent.new(Token.new("<test>", "G", 0, 0), 0, null), null, null).decl;

	var declE = desugaring.synthesizeTopLevelClass(null);
	var typeconE = V3Class_TypeCon.new(declE, typeCache);
	var variantE = ClassType.!(typeconE.create0());

	var typeconF = V3Class_TypeCon.new(declF, typeCache);
	CASE_F = ClassType.!(typeconF.create0());
	CASE_F.superType = variantE;  // TODO: internals exposed

	var typeconG = V3Class_TypeCon.new(declG, typeCache);
	CASE_G = ClassType.!(typeconG.create0());
	CASE_G.superType = variantE;  // TODO: internals exposed

	return variantE;
}

private class TypeSystemTester(t: Tester) {
	def assertRelation(f: (Type, Type) -> bool, op: string, x: bool, a: Type, b: Type) {
		if (f(a, b) != x) {
			var format = if(x,
				"type \"%q\" should be %s to \"%q\"",
				"type \"%q\" should not be %s to \"%q\"");
			t.fail3(format, a.render, op, b.render);
		}
	}
	def assertImplicit(x: bool, a: Type, b: Type) {
		return assertRelation(TypeSystem.isPromotable, "promotable", x, a, b);
	}
	def assertMaybeEqual(x: bool, a: Type, b: Type) {
		return assertRelation(TypeSystem.maybeEqual, "maybe equal", x, a, b);
	}
	def assertSubtype(x: bool, a: Type, b: Type) {
		return assertRelation(TypeSystem.isSubtype, "subtype", x, a, b);
	}
	def assertImplicitTuple(x: bool, a: Type, b: Type) {
		assertImplicit(x, a, b);
		assertImplicit(x, t2(a, VOID), t2(b, VOID));
		assertImplicit(x, t2(VOID, a), t2(VOID, b));
		assertImplicit(x, t2(a, a), t2(b, b));
		assertImplicit(x, t2(a, I32), t2(b, I64));
	}
	def assertUpper(x: Type, a: Type, b: Type) {
		var r1 = TypeSystem.upper(a, b);
		if (r1 != x) {
			var buf = StringBuilder.new();
			buf.puts("expected upper(")
				.put1("%q", renderer(a))
				.puts(", ")
				.put1("%q", renderer(b))
				.puts(") = ")
				.put1("%q", renderer(x))
				.puts(", got ")
				.put1("%q", renderer(r1));
			t.fail(buf.toString());
		}
		var r2 = TypeSystem.upper(b, a);
		if (r1 != r2) {
			var buf = StringBuilder.new();
			buf.put2("upper(%q, %q) != ",  renderer(a), renderer(b))
			   .put2("upper(%q, %q), got ", renderer(b), renderer(a))
			   .put2("%q and %q", renderer(r1), renderer(r2));
			t.fail(buf.toString());
		}
	}
	def assertUpperTuple(x: Type, a: Type, b: Type) {
		assertUpper(t2n(a, a), t2(a, a), t2(a, a));
		assertUpper(t2n(x, x), t2(a, a), t2(b, b));
		assertUpper(t2n(a, x), t2(a, a), t2(a, b));
		assertUpper(t2n(x, a), t2(a, a), t2(b, a));
		assertUpper(t2n(x, VOID), t2(a, VOID), t2(b, VOID));
		assertUpper(t2n(VOID, x), t2(VOID, a), t2(VOID, b));
	}
	def assertUpperFunction(x: Type, l: Type, a: Type, b: Type) {
		assertUpper(x, a, b);
		assertUpper(f1n(a, a), f1(a, a), f1(a, a));
		assertUpper(f1n(b, b), f1(b, b), f1(b, b));
		if (x == null) {
			assertUpper(null, f1(a, a), f1(b, b));
			assertUpper(null, f1(a, a), f1(a, b));
			assertUpper(x, f1(a, a), f1(b, a));
		} else {
			assertUpper(fn(l, VOID), f1(a, VOID), f1(b, VOID));
			assertUpper(f1(VOID, x), f1(VOID, a), f1(VOID, b));
		}
	}
	def assertTypeQuery(castOp: TypeQuery, a: Type, b: Type) {
		var t = TypeSystem.newTypeQuery(a, b);
		if (t != castOp) {
			var buf = StringBuilder.new();
			buf.put2("expected newTypeQuery(%q, %q) = ", a.render, b.render)
				.put2("%s, got %s", castOp.name, t.name);
			this.t.fail(buf.toString());
		}
	}
	def assertTypeCast(castOp: TypeCast, a: Type, b: Type) {
		var t = TypeSystem.newTypeCast(a, b);
		if (t != castOp) {
			var buf = StringBuilder.new();
			buf.put2("expected newTypeCast(%q, %q) = ", a.render, b.render)
				.put2("%s, got %s", castOp.name, t.name);
			this.t.fail(buf.toString());
		}
	}
	def assertWiden(expected: Type, a: Type, b: Type) {
		var got = TypeSystem.widen(a, b);
		if (expected != got) {
			var buf = StringBuilder.new();
			buf.put2("expected TypeSystem.widen(%q, %q) == ", a.render, b.render);
			if (expected == null) buf.puts("<null>");
			else expected.render(buf);
			buf.puts(", got ");
			if (got == null) buf.puts("<null>");
			else got.render(buf);
			t.fail(buf.toString());
		}
		var g2 = TypeSystem.widen(b, a);
		if (g2 != got) {
			var buf = StringBuilder.new();
			buf.put2("TypeSystem.widen(%q, %q) is not commutative", a.render, b.render);
			t.fail(buf.toString());
		}
	}
	// yyy
}

def testImplicit(t: TypeSystemTester) {
	def Y = t.assertImplicit(true, _, _);
	def N = t.assertImplicit(false, _, _);

	for (t in some) Y(t, t);

	Y(NULL, STRING);
	Y(NULL, CLASS_A);
	Y(NULL, CLASS_B);
	Y(CLASS_B, CLASS_A);
	Y(CLASS_C, CLASS_A);
	N(CLASS_A, CLASS_B);
	N(CLASS_X, CLASS_A);
	N(CLASS_A, CLASS_X);

	Y(CASE_F, VARIANT_E);
	Y(CASE_G, VARIANT_E);
	N(VARIANT_E, CASE_F);

	// Test implicit conversion between integer types
	for (i = 1; i < 64; i++) {
		var u1 = Int.getType(false, i), u2 = Int.getType(false, i+1);
		var s1 = Int.getType(true, i), s2 = Int.getType(true, i+1);
		Y(u1, u2);
		Y(s1, s2);
		Y(u1, s2);
		N(u2, s2);
		N(s2, u2);
		N(s1, u1);
		N(u2, u1);
		N(s2, s1);
	}
	// test implicit conversion between tuple types
	t.assertImplicitTuple(true, I32, I64);
	t.assertImplicitTuple(false, I64, I32);
	var tt = t;
	for (t in some) tt.assertImplicitTuple(true, t, t);

	// test implicit conversion between function types
	def fA = f1(VOID, CLASS_A);
	def fB = f1(VOID, CLASS_B);
	def fX = f1(VOID, CLASS_X);
	Y(fB, fA);
	N(fA, fB);
	N(fX, fA);
	N(fA, fX);

	// Test type parameters and type variables
	var types = [PARAM_P, PARAM_Q, VAR_V, VAR_W];
	for (t in types) for (u in types) {
		tt.assertImplicit(t == u, t, u);
	}
	// parameterized class types
	Y(CLASS_Z_P, CLASS_Y_P);
	Y(CLASS_Z_Q, CLASS_Y_Q);
	N(CLASS_Y_P, CLASS_Z_P);
	N(CLASS_Y_Q, CLASS_Z_P);
	N(CLASS_Y_P, CLASS_Z_Q);
	N(CLASS_Y_Q, CLASS_Z_Q);
	N(CLASS_Z_Q, CLASS_Y_P);
	N(CLASS_Z_P, CLASS_Y_Q);
	N(CLASS_Z_Q, CLASS_Z_P);
	N(CLASS_Z_P, CLASS_Z_Q);
	N(CLASS_Y_Q, CLASS_Y_P);
	N(CLASS_Y_P, CLASS_Y_Q);
}

def testSubtype(t: TypeSystemTester) {
	def Y = t.assertSubtype(true, _, _);
	def N = t.assertSubtype(false, _, _);

	Y(NULL, STRING);
	Y(NULL, CLASS_A);
	Y(NULL, CLASS_B);
	Y(CLASS_B, CLASS_A);
	Y(CLASS_C, CLASS_A);
	N(CLASS_A, CLASS_B);
	N(CLASS_X, CLASS_A);
	N(CLASS_A, CLASS_X);

	Y(CASE_F, VARIANT_E);
	Y(CASE_G, VARIANT_E);
	N(VARIANT_E, CASE_F);

	// Test subtype between integer types
	for (i = 1; i < 64; i++) {
		var u1 = Int.getType(false, i), u2 = Int.getType(false, i+1);
		var s1 = Int.getType(true, i), s2 = Int.getType(true, i+1);
		N(u1, u2);
		N(s1, s2);
		N(u1, s2);
		N(u2, s2);
		N(s2, u2);
		N(s1, u1);
		N(u2, u1);
		N(s2, s1);
	}

	var numerics = [FLOAT, DOUBLE, I32, I64];
	for (x in numerics) {
		for (y in numerics) {
			t.assertSubtype(x == y, x, y);
		}
	}

	// test subtyping between function types
	{
		def fA = f1(VOID, CLASS_A);
		def fB = f1(VOID, CLASS_B);
		def fX = f1(VOID, CLASS_X);
		Y(fB, fA); // covariant return
		N(fA, fB);
		N(fX, fA);
		N(fA, fX);
	}
	{
		def fA = f1(CLASS_A, VOID);
		def fB = f1(CLASS_B, VOID);
		def fX = f1(CLASS_X, VOID);
		Y(fA, fB); // contravariant param
		N(fB, fA);
		N(fX, fA);
		N(fA, fX);
	}

	// Test type parameters and type variables
	var types = [PARAM_P, PARAM_Q, VAR_V, VAR_W];
	var tt = t;
	for (t in types) for (u in types) {
		tt.assertSubtype(t == u, t, u);
	}
	// parameterized class types
	Y(CLASS_Z_P, CLASS_Y_P);
	Y(CLASS_Z_Q, CLASS_Y_Q);
	N(CLASS_Y_P, CLASS_Z_P);
	N(CLASS_Y_Q, CLASS_Z_P);
	N(CLASS_Y_P, CLASS_Z_Q);
	N(CLASS_Y_Q, CLASS_Z_Q);
	N(CLASS_Z_Q, CLASS_Y_P);
	N(CLASS_Z_P, CLASS_Y_Q);
	N(CLASS_Z_Q, CLASS_Z_P);
	N(CLASS_Z_P, CLASS_Z_Q);
	N(CLASS_Y_Q, CLASS_Y_P);
	N(CLASS_Y_P, CLASS_Y_Q);
}

def testUpper(t: TypeSystemTester) {
	def N = t.assertUpper(null, _, _);
	for (x in some) t.assertUpper(x, x, x);

	N(I32, I64);
	N(CLASS_X, CLASS_A);

	t.assertUpper(VOID, VOID, VOID);
	t.assertUpper(CLASS_A, CLASS_A, CLASS_B);
	t.assertUpper(CLASS_A, CLASS_A, CLASS_C);
	t.assertUpper(CLASS_A, CLASS_B, CLASS_C);

	t.assertUpper(VARIANT_E, VARIANT_E, CASE_G);
	t.assertUpper(VARIANT_E, CASE_F, CASE_G);

	for (c in classes) t.assertUpper(c, NULL, c);

	N(I32, BOOL);
	N(I32, CLASS_X);
	N(CLASS_X, PARAM_P);
	N(PARAM_P, PARAM_Q);
//	N(CLASS_X, VAR_V);
//	N(PARAM_P, VAR_V);

	t.assertUpperTuple(VOID, VOID, VOID);
	t.assertUpperTuple(null, I32, I64);
	t.assertUpperTuple(CLASS_A, CLASS_A, CLASS_B);
	t.assertUpperTuple(CLASS_A, CLASS_A, CLASS_C);
	t.assertUpperTuple(CLASS_A, CLASS_B, CLASS_C);
	t.assertUpperTuple(null, CLASS_X, CLASS_A);

	// upper, lower, a, b
	t.assertUpperFunction(VOID, VOID, VOID, VOID);
	t.assertUpperFunction(null, null, I32, I64);
	t.assertUpperFunction(CLASS_A, CLASS_B, CLASS_A, CLASS_B);
	t.assertUpperFunction(CLASS_A, CLASS_C, CLASS_A, CLASS_C);
	t.assertUpperFunction(CLASS_A, null, CLASS_B, CLASS_C);
	t.assertUpperFunction(null, null, CLASS_X, CLASS_A);

	// parameterized class types
	t.assertUpper(CLASS_Y_P, CLASS_Z_P, CLASS_Y_P);
	t.assertUpper(CLASS_Y_Q, CLASS_Z_Q, CLASS_Y_Q);
	N(CLASS_Y_Q, CLASS_Z_P);
	N(CLASS_Y_P, CLASS_Z_Q);
	N(CLASS_Z_Q, CLASS_Y_P);
	N(CLASS_Z_P, CLASS_Y_Q);
	N(CLASS_Z_Q, CLASS_Z_P);
	N(CLASS_Z_P, CLASS_Z_Q);
	N(CLASS_Y_Q, CLASS_Y_P);
	N(CLASS_Y_P, CLASS_Y_Q);
}

def testWiden(t: TypeSystemTester) {
	// TODO
}

def testCommonSuperClass(t: TypeSystemTester) {
	// TODO
}

def testUnify(t: TypeSystemTester) {
	// TODO
}

def testUnifyWiden(t: TypeSystemTester) {
	// TODO
}

def testNewTypeQuery(t: TypeSystemTester) {
	def Y = t.assertTypeQuery(TypeQuery.TRUE, _, _);
	def N = t.assertTypeQuery(TypeQuery.FALSE, _, _);
	def M = t.assertTypeQuery(TypeQuery.UNKNOWN_QUERY, _, _);
	for (p in prim) {
		Y(p, p);
		for (q in some) {
			if (p == q || true) continue; // TODO
			N(p, q);
			N(q, p);
		}
	}
	for (c in classes) {
		N(c, NULL);
		N(NULL, c);
		M(PARAM_P, c);
		M(c, PARAM_Q);
		t.assertTypeQuery(TypeQuery.TRUE_IF_NOT_NULL, c, c);
	}
	var A = CLASS_A, B = CLASS_B, C = CLASS_C, X = CLASS_X;
	t.assertTypeQuery(TypeQuery.CLASS_QUERY, A, B);
	t.assertTypeQuery(TypeQuery.TRUE_IF_NOT_NULL, B, A);
	t.assertTypeQuery(TypeQuery.FALSE, B, C);
	t.assertTypeQuery(TypeQuery.FALSE, A, X);
	t.assertTypeQuery(TypeQuery.FALSE, X, A);

	var E = VARIANT_E, F = CASE_F, G = CASE_G;
	t.assertTypeQuery(TypeQuery.VARIANT_QUERY, E, F);
	t.assertTypeQuery(TypeQuery.TRUE, F, E);
	t.assertTypeQuery(TypeQuery.FALSE, F, G);
	t.assertTypeQuery(TypeQuery.FALSE, A, F);
	t.assertTypeQuery(TypeQuery.FALSE, E, A);

	N(t2(A, A), t2(A, X));
	N(t2(A, A), t2(X, A));
	N(t2(A, X), t2(A, A));
	N(t2(X, A), t2(A, A));

	N(A, t2(A, A));
	N(t2(A, A), A);
	N(t3(A, A, A), t2(A, A));
	N(t2(A, A), t3(A, A, A));

	t.assertTypeQuery(TypeQuery.TUPLE_QUERY, t2(A, A), t2(A, B));
	t.assertTypeQuery(TypeQuery.TUPLE_QUERY, t2(A, A), t2(B, B));
	t.assertTypeQuery(TypeQuery.TUPLE_QUERY, t2(A, B), t2(B, B));

	def aOf = V3Array.newType;
	def AA = aOf(A), AB = aOf(B), AX = aOf(X);

	t.assertTypeQuery(TypeQuery.TRUE_IF_NOT_NULL, AA, AA);
	N(AA, AB);
	N(AB, AA);
	N(AX, AA);
	N(NULL, AA);
	N(AA, NULL);

	// parameterized class types
	var Y_P = CLASS_Y_P, Y_Q = CLASS_Y_Q, Z_P = CLASS_Z_P, Z_Q = CLASS_Z_Q;
	t.assertTypeQuery(TypeQuery.CLASS_QUERY, Y_P, Z_P);
	t.assertTypeQuery(TypeQuery.UNKNOWN_QUERY, Y_P, Z_Q);
	t.assertTypeQuery(TypeQuery.TRUE_IF_NOT_NULL, Z_P, Y_P);
	t.assertTypeQuery(TypeQuery.UNKNOWN_QUERY, Z_Q, Y_P);
	t.assertTypeQuery(TypeQuery.UNKNOWN_QUERY, Y_P, Y_Q);
	t.assertTypeQuery(TypeQuery.UNKNOWN_QUERY, Y_Q, Y_P);
	t.assertTypeQuery(TypeQuery.UNKNOWN_QUERY, Z_P, Z_Q);
	t.assertTypeQuery(TypeQuery.UNKNOWN_QUERY, Z_Q, Z_P);
	t.assertTypeQuery(TypeQuery.FALSE, Y_P, C);
	t.assertTypeQuery(TypeQuery.FALSE, Y_Q, X);
	t.assertTypeQuery(TypeQuery.FALSE, X, Y_P);

}
def testNewTypeCast(t: TypeSystemTester) {
	def Y = t.assertTypeCast(TypeCast.TRUE, _, _);
	def N = t.assertTypeCast(TypeCast.THROW, _, _);
	def M = t.assertTypeCast(TypeCast.UNKNOWN_CAST, _, _);
	def T = t.assertTypeCast(TypeCast.TUPLE_CAST, _, _);
	for (p in prim) {
		Y(p, p);
		t.assertTypeCast(TypeCast.THROW, p, Void.TYPE);
	}
	var ints = [I32, I64];
	var tt = t;
	for (t in ints) {
		for (u in ints) {
			if (t == u) Y(t, u);
			else if (t.width < u.width) tt.assertTypeCast(TypeCast.INT_VIEW_I, t, u);
			else tt.assertTypeCast(TypeCast.INT_CAST_I, t, u);
		}
	}
	for (c in classes) {
		Y(c, c);
		N(c, NULL);
		Y(NULL, c);
		M(PARAM_P, c);
		M(c, PARAM_Q);
		t.assertTypeCast(TypeCast.THROW, c, Void.TYPE);
	}
	var A = CLASS_A, B = CLASS_B, C = CLASS_C, X = CLASS_X;
	t.assertTypeCast(TypeCast.CLASS_CAST, A, B);
	t.assertTypeCast(TypeCast.TRUE, B, A);
	t.assertTypeCast(TypeCast.THROW_IF_NOT_NULL, B, C);
	t.assertTypeCast(TypeCast.THROW_IF_NOT_NULL, A, X);
	t.assertTypeCast(TypeCast.THROW_IF_NOT_NULL, X, A);

	var E = VARIANT_E, F = CASE_F, G = CASE_G;
	t.assertTypeCast(TypeCast.VARIANT_CAST, E, F);
	t.assertTypeCast(TypeCast.TRUE, F, E);
	t.assertTypeCast(TypeCast.THROW, F, G);
	t.assertTypeCast(TypeCast.THROW, A, F);
	t.assertTypeCast(TypeCast.THROW, E, A);

	T(t2(A, A), t2(A, X));
	T(t2(A, A), t2(X, A));
	T(t2(A, X), t2(A, A));
	T(t2(X, A), t2(A, A));

	N(A, t2(A, A));
	N(t2(A, A), A);
	N(t3(A, A, A), t2(A, A));
	N(t2(A, A), t3(A, A, A));

	t.assertTypeCast(TypeCast.TUPLE_CAST, t2(A, A), t2(A, B));
	t.assertTypeCast(TypeCast.TUPLE_CAST, t2(A, A), t2(B, B));
	t.assertTypeCast(TypeCast.TUPLE_CAST, t2(A, B), t2(B, B));

	def aOf = V3Array.newType;
	def AA = aOf(A), AB = aOf(B), AX = aOf(X);

	Y(AA, AA);
	Y(NULL, AA);
	N(A, AB);
	N(B, AA);
	N(X, AA);
	N(A, NULL);

	// parameterized class types
	var Y_P = CLASS_Y_P, Y_Q = CLASS_Y_Q, Z_P = CLASS_Z_P, Z_Q = CLASS_Z_Q;
	t.assertTypeCast(TypeCast.CLASS_CAST, Y_P, Z_P);
	t.assertTypeCast(TypeCast.UNKNOWN_CAST, Y_P, Z_Q);
	t.assertTypeCast(TypeCast.TRUE, Z_P, Y_P);
	t.assertTypeCast(TypeCast.UNKNOWN_CAST, Z_Q, Y_P);
	t.assertTypeCast(TypeCast.UNKNOWN_CAST, Y_P, Y_Q);
	t.assertTypeCast(TypeCast.UNKNOWN_CAST, Y_Q, Y_P);
	t.assertTypeCast(TypeCast.UNKNOWN_CAST, Z_P, Z_Q);
	t.assertTypeCast(TypeCast.UNKNOWN_CAST, Z_Q, Z_P);
	t.assertTypeCast(TypeCast.THROW_IF_NOT_NULL, Y_P, C);
	t.assertTypeCast(TypeCast.THROW_IF_NOT_NULL, Y_Q, X);
	t.assertTypeCast(TypeCast.THROW_IF_NOT_NULL, X, Y_P);
}

def testMaybeEqual(t: TypeSystemTester) {
	def Y = t.assertMaybeEqual(true, _, _);
	def N = t.assertMaybeEqual(false, _, _);
	for (p in some) {
		Y(p, p);
		Y(PARAM_P, p);
		Y(VAR_V, p);
		for (q in some) {
			if (p == q) continue;
			N(p, q);
			N(q, p);
		}
	}
	for (c in classes) {
		Y(c, c);
		Y(PARAM_P, c);
		Y(c, PARAM_Q);
		Y(VAR_V, c);
		Y(c, VAR_W);
	}
	var A = CLASS_A, fAV = f1(A, VOID), fVA = f1(VOID, A);
	Y(fAV, fAV);
	N(fVA, fAV);
	var B = CLASS_B, fBV = f1(B, VOID), fVB = f1(VOID, B);
	N(fAV, fBV);
	N(fVA, fBV);

	var tt = t;
	for (t in some) {
		for (u in some) {
			tt.assertMaybeEqual(t == u, t2(t, u), t2(t, t));
			Y(t2(t, u), t2(t, VAR_V));
			Y(t2(u, t), t2(VAR_V, t));
		}
	}

	// parameterized class types
	var ys = [CLASS_Y_P, CLASS_Y_Q], zs = [CLASS_Z_P, CLASS_Z_Q];
	for (y1 in ys) {
		for (y2 in ys) Y(y1, y2);
		N(CLASS_Z_P, y1);
		N(y1, CLASS_Z_P);
	}
	for (z1 in zs) {
		for (z2 in zs) Y(z1, z2);
		N(CLASS_Y_P, z1);
		N(z1, CLASS_Y_P);
	}
}

def testFloats(t: TypeSystemTester) {
	def Y = t.assertImplicit(true, _, _);
	def N = t.assertImplicit(false, _, _);
	def W = t.assertWiden;

	Y(FLOAT, DOUBLE);
	N(DOUBLE, FLOAT);
	Y(Byte.TYPE, FLOAT);

	for (i = 1; i <= 64; i++) {
		t.assertImplicit(i < 25, Int.getType(false, i), FLOAT);
		t.assertImplicit(i < 24, Int.getType(true, i), FLOAT);
		t.assertImplicit(i < 54, Int.getType(false, i), DOUBLE);
		t.assertImplicit(i < 53, Int.getType(true, i), DOUBLE);
	}

	W(DOUBLE, FLOAT, DOUBLE);
	W(null, FLOAT, NULL);
	W(null, DOUBLE, BOOL);
	W(null, DOUBLE, VOID);

	W(FLOAT, Byte.TYPE, FLOAT);
	W(DOUBLE, Int.TYPE, DOUBLE);
}
